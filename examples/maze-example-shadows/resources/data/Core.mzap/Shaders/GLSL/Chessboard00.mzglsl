//////////////////////////////////////////
// Vertex Shader
//
//////////////////////////////////////////
#type vertex

//////////////////////////////////////////
#include "Utils/PrecisionHigh.mzglsl"
#include "Utils/Vertex.mzglsl"


// SHADOW_MAP
uniform sampler2D u_mainLightShadowMap;
uniform mat4 u_mainLightViewProjectionMatrix;


//////////////////////////////////////////
IN vec3 a_position;
IN vec3 a_normal;
IN vec3 a_tangent;
IN vec3 a_bitangent;
IN vec4 a_color;
IN vec2 a_texCoords0;


//////////////////////////////////////////
OUT vec3 v_positionOS;
OUT vec3 v_positionWS;
OUT vec3 v_positionVS;
OUT vec3 v_normalOS;
OUT vec3 v_normalWS;
OUT vec3 v_tangentOS;
OUT vec3 v_tangentWS;
OUT vec3 v_bitangentOS;
OUT vec3 v_bitangentWS;
OUT vec4 v_color;
OUT vec2 v_uv0;
OUT vec3 v_viewDirWS;
OUT vec4 v_positionMLS;
OUT vec2 v_mainLightShadowMapTexelSize;


//////////////////////////////////////////
#include "Utils/Core.mzglsl"


//////////////////////////////////////////
// Main
void main()
{
    
    int instanceId = gl_InstanceID;
    
    mat4 modelMatrix = GetModelMatrix(instanceId);
    mat4 viewMatrix = GetViewMatrix();

    vec4 positionOS = vec4(a_position, 1.0);
    vec4 positionWS = modelMatrix * positionOS;
    MAZE_CLIP_DISTANCE_VERTEX(positionWS);
    vec4 positionVS = viewMatrix * positionWS;
    vec4 positionCS = u_projectionMatrix * positionVS;
    
    vec4 normalOS = vec4(a_normal, 1.0);
    vec4 tangentOS = vec4(a_tangent, 1.0);
    vec4 bitangentOS = vec4(a_bitangent, 1.0);
    
    mat3 normalMatrix = mat3(modelMatrix);
    
        
#if (MAZE_LHCS)
    positionCS.z = -positionCS.z;
#endif
    
    v_positionOS = positionOS.xyz;
    v_positionWS = positionWS.xyz;
    v_positionVS = positionVS.xyz;
    
    vec3 normalWS = normalize(normalMatrix * normalOS.xyz).xyz;
        
    v_normalOS = normalOS.xyz;
    v_normalWS = normalWS.xyz;
    
    v_color = a_color;
       
    v_uv0 = a_texCoords0;

    v_viewDirWS = u_viewPosition - v_positionWS;
    
    v_positionMLS = u_mainLightViewProjectionMatrix * positionWS;
    v_mainLightShadowMapTexelSize = 1.0 / textureSize(u_mainLightShadowMap, 0);

    gl_Position = positionCS;
}


//////////////////////////////////////////
// Fragment Shader
//
//////////////////////////////////////////
#type fragment

//////////////////////////////////////////
#include "Utils/PrecisionHigh.mzglsl"
#include "Utils/Fragment.mzglsl"
#include "Utils/Core.mzglsl"
#include "Utils/Lighting.mzglsl"


//////////////////////////////////////////
uniform vec4 u_color;
uniform vec4 u_ambientLightColor;
uniform float u_shininess;
uniform vec4 u_specularColor;

// SHADOW_MAP
uniform sampler2D u_mainLightShadowMap;
uniform float u_shadowMult;

// LAMBERT_THRESHOLD
uniform float u_lambertThreshold;

// Checkerboard
uniform float u_checkerScale;
uniform vec4 u_checkerColor0;
uniform vec4 u_checkerColor1;
uniform vec4 u_checkerColor0b;
uniform vec4 u_checkerColor1b;


//////////////////////////////////////////
IN vec3 v_positionOS;
IN vec3 v_positionWS;
IN vec3 v_positionVS;
IN vec3 v_normalOS;
IN vec3 v_normalWS;
IN vec3 v_tangentOS;
IN vec3 v_tangentWS;
IN vec3 v_bitangentOS;
IN vec3 v_bitangentWS;
IN vec4 v_color;
IN vec2 v_uv0;
IN vec3 v_viewDirWS;
IN vec4 v_positionMLS;
IN vec2 v_mainLightShadowMapTexelSize;


//////////////////////////////////////////
MAZE_LAYOUT_LOCATION(0) out vec4 out_color;


//////////////////////////////////////////
// BlinnPhong
vec3 BlinnPhongLighting(
    vec3 diffuseColor,
    vec3 normalWS,
    vec3 fragmentToViewDirection,
    vec3 lightDirection,
    vec3 lightColor)
{
    // Light
    vec3 fragmentToLightDirection = -lightDirection;
    float lightAttenuation = 1.0;
    
    // Lambertian
    float NdotL = dot(normalWS, fragmentToLightDirection);
    float lambertian = clamp(NdotL, 0.0, 1.0);
    
    // Lambertian threshold
#if defined(LAMBERT_THRESHOLD) && (LAMBERT_THRESHOLD)
    lambertian = u_lambertThreshold + lambertian * (1.0f - u_lambertThreshold);
#endif
    
    // Light final
    vec3 lightColorFinal = lightColor * lightAttenuation;
    
    // Half vector
    vec3 H = normalize(fragmentToLightDirection + fragmentToViewDirection);
    
    // Shininess
    float shininess = u_shininess;

    // Specular
    float NdotH = dot(normalWS, H);    
    float specular = pow(clamp(NdotH, 0.0, 1.0), shininess * 512.0);
    vec3 specularColor = u_specularColor.rgb * specular;    
    
    return 
        diffuseColor * lambertian * lightColorFinal +
        specularColor * lightColorFinal;
}


//////////////////////////////////////////
float Checkerboard3d(vec3 positionWS)
{
    vec3 bias = vec3(0.0001);
    
    vec3 f = floor(positionWS + bias);
    float value = clamp(mod(f.x + f.y + f.z, 2.0), 0.0, 1.0);
        
    return value;
}

//////////////////////////////////////////
// Main
void main()
{
    MAZE_CLIP_DISTANCE_FRAGMENT;
    
    // View direction
    vec3 fragmentToViewDirection = normalize(v_viewDirWS);

    // Normal
    vec3 normalWS = normalize(v_normalWS);
    
    // Ambient light
    vec3 ambientLightColor = u_ambientLightColor.rgb;
        
    // Checkerboard
    float check0 = Checkerboard3d(v_positionWS * u_checkerScale);
    vec3 checkColor0 = mix(u_checkerColor0.xyz, u_checkerColor1.xyz, check0);
    
    vec3 checkerColor2 = mix(u_checkerColor0b.xyz, u_checkerColor1b.xyz, check0);
    float check1 = Checkerboard3d(v_positionWS * u_checkerScale * 4.0);   
    
    // Diffuse color
    vec3 diffuseColor = mix(checkColor0, checkerColor2, check1);
        
    // Linear color
    vec3 linearColor = diffuseColor * ambientLightColor;
            
    // Main light
    float mainLightShadow = CalculateShadow(v_positionMLS, normalWS, u_mainLightDirection, u_mainLightShadowMap, v_mainLightShadowMapTexelSize) * u_shadowMult;
    linearColor += (1.0 - mainLightShadow) * BlinnPhongLighting(diffuseColor, normalWS, fragmentToViewDirection, normalize(u_mainLightDirection), u_mainLightColor.rgb);
    
    // Reset to LDR
    linearColor.r = min(linearColor.r, 1.0);
    linearColor.g = min(linearColor.g, 1.0);
    linearColor.b = min(linearColor.b, 1.0);
    
        
    out_color = vec4(linearColor, 1.0);
}